pre beginFiltering {
	"Filtering Rules Start:-----".println();
}
/* 
	R1: If there is an activity or control Node (Not Object Node) with one incoming and outgoing edge, it can be deleted.
*/
pattern SingleInputOutputNonObjectNode
	flowNode: FlowNode
		guard: not flowNode.isTypeOf(ObjectNode) and flowNode.incoming.size() = 1 and flowNode.outgoing.size() = 1
	--inEdge: Edge[1]
	--	from: flowNode.incoming,
	--outEdge: Edge[1]
	--	from: flowNode.outgoing
{
	/* match {
		"".println("R1 match..");
		return true;
	}*/
	onmatch {
		flowNode.println("R1 Matched..");
		
	}
	do {
		"".println("R1 do..");
		--inEdge.targetRef = outEdge.targetRef;
		flowNode.incoming.first().targetRef = flowNode.outgoing.first().targetRef;
		--delete outEdge;
		delete flowNode.outgoing;
		delete flowNode;
		"".println("R1 applied..");
	}
}

/* 
	R2: If there is an XOR Gateway(Decision) with a single incoming Edge from another XOR Gateway(Decision) , the two can be merged (preserving the guards).
*/
pattern MergeDivergingExclusiveGateway
	secondGateway: ExclusiveGateway
		guard: secondGateway.incoming.size() = 1 and secondGateway.outgoing.size() > 1, //added for conlict with R1
	edge: Edge
		from: secondGateway.incoming
		guard: edge.sourceRef.isTypeOf(ExclusiveGateway),
	firstGateway: ExclusiveGateway
		from: edge.sourceRef
		guard: firstGateway.outgoing.size() > 1		//added + changed again so to be different from R3 for many -->  one --> many
{
	onmatch {
		edge.println("R2 Matched..");
		
	}
	do {
		"".println("R2 do..");
		for (_edge in secondGateway.outgoing.clone()) {
			//_edge.id = loopCount.asString();
			//need to use clone() for below, but not above!!??
			_edge.sourceRef = firstGateway;
			//_edge.guard += "& " + edge.guard; //preserve guard???
		}
		delete secondGateway;
		delete edge;
		firstGateway.println("R2 applied..");
	}
}

/*
	R3: If there is an XOR Gateway(Merge) with a single outgoing Edge to another XOR Gateway, the two can be merged (preserving the guards).
*/
pattern MergeConvergingExclusiveGateway
	firstGateway: ExclusiveGateway
		guard: firstGateway.outgoing.size() = 1 and firstGateway.incoming.size() > 1, //added to avoid conflict with R1
	edge: Edge
		from: firstGateway.outgoing
		guard: edge.targetRef.isTypeOf(ExclusiveGateway),
	secondGateway: ExclusiveGateway
		from: edge.targetRef
		guard: secondGateway.incoming.size() > 1 or secondGateway.outgoing.size() > 1	//added	
{
	onmatch {
		edge.println("R3 Matched..");
		
	}
	do {
		"".println("R3 do..");
		for (_edge in firstGateway.incoming.clone()) {
			_edge.targetRef = secondGateway; 
		}
		delete firstGateway;
		delete edge;
		secondGateway.println("R3 applied..");
	}
}

/* 
	R4: If there is an XOR Gateway with a self loop Edge, the edge can be deleted.
*/
pattern DeleteLoopEdgeExclusiveGateway
	gateway: ExclusiveGateway,
	edge: Edge
		from: gateway.outgoing
		guard: edge.targetRef = edge.sourceRef
{
	onmatch {
		edge.println("R4 Matched..");
		
	}
	do {
		"".println("R4 do..");
		delete edge;
		gateway.println("R4 applied..");
	}
}


/* 
	R5: If there is an AND Gateway (Parallel) connected directly to another AND Gateway and also through a path of 2 or more nodes, the direct edge can be deleted.
*/
pattern DeleteRedundantEdgeParallelGateway
	firstGateway: ParallelGateway,
	//simple path  from firstGateway to secondGateway??
	edge: Edge
		from: firstGateway.outgoing,
	secondGateway: ParallelGateway
		from: edge.targetRef
		guard: SimplePathBetweenNodes?(firstGateway, secondGateway)
{
	onmatch {
		edge.println("R5 Matched..");
		
	}
	do {
		"".println("R5 do..");
		delete edge;
		firstGateway.println("R5 applied..");
	}
}
 
/* 
	R6: If there is an AND Gateway with more than one outgoing edges and one is connected to an end Event (final node), the edge and final node can be deleted.
*/
pattern DeleteEndEventFromParallelGateway
	gateway: ParallelGateway
		guard: gateway.outgoing.size() > 1,
	edge: Edge
		from: gateway.outgoing,
	endEvent: EndEvent
		from: edge.targetRef
{
	onmatch {
		endEvent.println("R6 Matched..");
		
	}
	do {
		"".println("R6 do..");
		delete edge;
		delete endEvent;
		gateway.println("R6 applied..");
	}
}

/***
	need to check for this situation:
1) 
	 --->|       |-----> 
	     |------>|
	 --->|       |----->

2)
	 	 |------>| 
	 --->|		 |----->
	 	 |------>|

3)
	---> /\	______ /\---->		 		  		
	---> \/	       \/---->
	 	
***/

/* 
	R7: If there is an AND Gateway(Parallel) with a single incoming Edge from another AND Gateway(Fork) with multiple outgoing edges, the two can be merged.
*/
pattern MergeDivergingParallelGateway
	secondGateway: ParallelGateway
		guard: secondGateway.incoming.size() = 1 and secondGateway.outgoing.size() > 1, //added outgoing so not to match with R1,
	edge: Edge
		from: secondGateway.incoming
		guard: edge.sourceRef.isTypeOf(ParallelGateway),
	firstGateway: ParallelGateway
		from: edge.sourceRef
		guard: firstGateway.outgoing.size() > 1 //no need!!  needed again		
{
	onmatch {
		edge.println("R7 Matched..");
		
	}
	do {
		"".println("R7 do..");
		for (_edge in secondGateway.outgoing.clone()) {
			_edge.sourceRef = firstGateway; 
		}
		delete secondGateway;
		delete edge;
		firstGateway.println("R7 applied..");
	}
}

/* 
	R8: If there is an AND Gateway(Join) with a single outgoing Edge to another AND Gateway with more than one incoming edge, the two can be merged.
*/
pattern MergeConvergingParallelGateway
	firstGateway: ParallelGateway
		guard: firstGateway.outgoing.size() = 1 and firstGateway.incoming.size() > 1, //added incoming so not to match with R1 
	edge: Edge
		from: firstGateway.outgoing
		guard: edge.targetRef.isTypeOf(ParallelGateway),
	secondGateway: ParallelGateway
		from: edge.targetRef
		guard: secondGateway.incoming.size() > 1		//no need!! -- needed again 
{
	onmatch {
		edge.println("R8 Matched..");
		
	}
	do {
		"".println("R8 do..");
		for (_edge in firstGateway.incoming.clone()) {
			_edge.targetRef = secondGateway; 
		}
		delete firstGateway;
		delete edge;
		secondGateway.println("R8 applied..");
	}
}

/*
	R9: If there is an XOR Gateway(Decision) and an Object Node with same incoming Edge from a fork and same outgoing Edges to another AND Gateway(join), the XOR node can be deleted (preserving the guards).
*/
pattern DeleteRedundantDecisionExclusiveGateway
	XORGateway: ExclusiveGateway
		guard: XORGateway.incoming.size() = 1 and XORGateway.outgoing.forAll(e | e.targetRef.isTypeOf(ParallelGateway)),
	object: ObjectNode
		guard: object.incoming.size() = 1 and XORGateway.outgoing.size() = object.outgoing.size() and object.outgoing.collect(o1 | o1.targetRef).asSet().equals(XORGateway.outgoing.collect(o2 | o2.targetRef).asSet()), //object.outgoing = XORGateway.outgoing, //Collection equality and 
	forkGateway: ParallelGateway[1]
		from: XORGateway.incoming.sourceRef 
		guard: object.incoming.first().sourceRef = forkGateway //Removed check for Diverging!!
		--guard: forkGateway.gatewayDirection = GatewayDirection#Diverging and object.incoming.first().sourceRef = forkGateway
{
 	/*  match :
		XORGateway.incoming.size() = 1 and 
		XORGateway.outgoing.forAll(e | e.targetRef.isTypeOf(ParallelGateway)) and
		object.incoming.size() = 1 and 
		XORGateway.outgoing.size() = object.outgoing.size() and 
		object.outgoing.collect(o1 | o1.targetRef).asSet().equals(XORGateway.outgoing.collect(o2 | o2.targetRef).asSet())
	*/	 
	
	
	onmatch {
		XORGateway.println("R9 Matched..");
		--object.outgoing.println();
		--object.outgoing.collect(o1 | o1.targetRef).println();
		--XORGateway.outgoing.println();
		--XORGateway.outgoing.collect(o1 | o1.targetRef).println();
		//not the best way!!		
		
	}
	do {
		"".println("R9 do..");
		delete object.outgoing;
		for (_edge in XORGateway.outgoing.clone()) {
			_edge.sourceRef = object;
			--_edge.println(); 
		}
		
		delete XORGateway.incoming;
		delete XORGateway;
		object.println("R9 applied..");
	}
}

/* 
	R10: If there is an XOR Gateway(Mereg) and an Object Node with one outgoing Edge to a join and same incoming Edges from other AND Gateway(fork), the XOR node can be deleted.
*/
pattern DeleteRedundantMergeExclusiveGateway
	XORGateway: ExclusiveGateway
		guard: XORGateway.outgoing.size() = 1 and XORGateway.incoming.forAll(e | e.sourceRef.isTypeOf(ParallelGateway)),
	object: ObjectNode
		guard: object.outgoing.size() = 1 and XORGateway.incoming.size() = object.incoming.size() and object.incoming.collect(o1 | o1.sourceRef).asSequence() = XORGateway.incoming.collect(o2 | o2.sourceRef).asSequence(), //Collection equality (Bag, Sequence?)
	joinGateway: ParallelGateway[1]
		from: XORGateway.outgoing.targetRef 
		guard: object.outgoing.first().targetRef = joinGateway //removed check for Converging
		--guard: joinGateway.gatewayDirection = GatewayDirection#Converging and object.outgoing.first().targetRef = joinGateway
{
	onmatch {
		XORGateway.println("R10 Matched..");
		//object.incoming.collect(o1 | o1.sourceRef).println(); XORGateway.incoming.collect(o2 | o2.sourceRef).println();
		
	}
	do {
		"".println("R10 do..");
		delete XORGateway.incoming;
		delete XORGateway.outgoing;
		delete XORGateway;
		object.println("R10 applied..");
	}
}

/*
	Delete Redundant edges where there is another edge connecting same nodes. (check if Object Nodes!!??)
*/
pattern RemoveRedundantEdges
	flowNode1: FlowNode
		guard: flowNode1.outgoing.size() > 1,
	flowNode2: FlowNode,
	redundant_edge : Edge [2 .. *]
		from: flowNode1.outgoing 
		guard: redundant_edge.targetRef = flowNode2
	
{	
	onmatch {
		redundant_edge.println("R11 matched..");
	}
	
	do {
		"".println("R11 do..");
		--var edge = redundant_edge.at(0);
		--edge.println();
		var edge = redundant_edge.removeAt(0);
		edge.println();
		delete redundant_edge;
		flowNode1.outgoing.println();
		edge.println("R11 applied..");
	}
}
post endFiltering {
	"Filtering Rules End:-----".println();
}

/***************************************************** Helper Functions ***********************************/
/* 
	Get all the incoming nodes to the given node.
	(All nodes which are sourceRef to Edges which have the given FlowNode as targetRef)
*/
operation FlowNode getIncomingNodes() : Sequence(FlowNode) {
	_edges = Edge.all.select(e | e.targerRef = self);
	return FlowNode.all.select(n | _edges.exists(e | e.sourceRef = n));
}

/*
	Get all the outgoing nodes for the given node.
	(All nodes which are targetRef to Edges which have the given FlowNode as sourceRef)
*/
operation FlowNode getOutgoingNodes() : Sequence(FlowNode) {
	_edges = Edge.all.select(e | e.sourceRef = self);
	return FlowNode.all.select(n | _edges.exists(e | e.targetRef = n));
}

/* 
	Simple Path of length 2between first and second gateway
*/
operation SimplePathBetweenNodes?(first: ParallelGateway, second: ParallelGateway) : Boolean {
	var queue = first.outgoing.collect(e | e.targetRef).flatten;
	queue.remove(second);
	while (queue.size() > 0) {
		var node = queue.first();
		var outgoingNodes = node.outgoing.collect(e | e.targetRef).flatten.select(n | n.~visited <> true);
		if (outgoingNodes.includes(second))
			return true;
		else {
			node.~visited = true;
			queue.removeAt(0);
			queue.addAll(outgoingNodes);
		}
	} 
	return false;
}

operation Collection replaceAll(first: FlowNode, second: FlowNode) : Boolean {
	//for (item in self)
}